--USE [Datamart]
--GO

--/****** Object:  StoredProcedure [dbo].[usp_BOE_BILLING_FINISH]    Script Date: 8/26/2025 8:44:37 AM ******/
--SET ANSI_NULLS ON
--GO

--SET QUOTED_IDENTIFIER ON
--GO





-- =============================================
-- Author:        traci treffert
-- Create date: 9/13/2019
-- Description:    Boeing Billing for BOE609 Finish
--        converted from original code in Crystal Reports to be used as SSRS report
--        take the logic/process out of hardcoded CR report and put in SQL for ease of maintenance
--        logic to also be included in Gross Margin processing
-- =============================================

--DECLARE @STARTDATE DATE = '2025-08-18';
--DECLARE @ENDDATE DATE = '2025-08-24';

--CREATE PROCEDURE [dbo].[usp_BOE_BILLING_FINISH] (@STARTDATE DATE, @ENDDATE DATE) 
--AS

SET NOCOUNT ON;
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
SET DEADLOCK_PRIORITY LOW

DECLARE @CHARGE DECIMAL(14, 4) = (SELECT CHARGE FROM [SQL-BI-1].LIVESupplemental.dbo.[BOE_Billing_Charges] WHERE [REPORT] = 'usp_BOE_BILLING_FINISH')
;

SELECT SPROC 
    , [COST ITEM]
    , [POS]
    , [PART NUMBER]
    , [RAMAC]
    , [BILLED QTY]
    , CASE
        WHEN [BILLED QTY] <= 0 THEN 0.
        WHEN [Description] LIKE 'Stanchion%' THEN [Finish_Unit Run Hours] * [BILLED QTY] * @CHARGE / ISNULL([BILLED QTY], 1)
        WHEN [Description] LIKE 'Cargo Panel%' THEN [Finish_Unit Run Hours] * [BILLED QTY] * @CHARGE / ISNULL([BILLED QTY], 1)
        ELSE (SUM(SORT_SETUP) + SUM(SORT_RUN)) * @CHARGE / ISNULL([BILLED QTY], 1)
    END AS [UNIT PRICE]
    , CASE 
        WHEN [BILLED QTY] <= 0. THEN 0.
        WHEN [Description] LIKE 'Stanchion%' THEN [Finish_Unit Run Hours] * [BILLED QTY] * @CHARGE
        WHEN [Description] LIKE 'Cargo Panel%' THEN [Finish_Unit Run Hours] * [BILLED QTY] * @CHARGE
        ELSE (SUM(SORT_SETUP) + SUM(SORT_RUN)) * @CHARGE
    END AS [EXTENDED DOLLARS]
    , [EXPEDITE DOLLARS]
    , [SPECIAL FEES]
    , 0 AS [SERVICE_FEES]
    , CASE 
        WHEN [Description] IS NOT NULL THEN 0.
        ELSE SUM(SORT_SETUP)
    END AS [SETUP HOURS]
    , CASE 
        WHEN [BILLED QTY] <= 0. THEN 0.
        WHEN [Description] LIKE 'Stanchion%' THEN [Finish_Unit Run Hours] * [BILLED QTY]
        WHEN [Description] LIKE 'Cargo Panel%' THEN [Finish_Unit Run Hours] * [BILLED QTY]
        ELSE SUM(SORT_RUN) 
    END AS [RUN HOURS]
    , [EXPEDITE HOURS]
    , [PACK SLIPS / DATE]  
    , INVOICE_ID AS [INVOICE ID]
    , COMMENTS
    , '' AS SORTATION

FROM 
    (SELECT 'FIN' AS SPROC 
        , 0 AS [COST ITEM]
        , '1' AS [POS]
        , COL.PART_ID AS [PART NUMBER]
        , CO.ID AS [RAMAC]
        , CONVERT(DECIMAL(14, 4), COL.TOTAL_SHIPPED_QTY) AS [BILLED QTY]
        , 0 AS [EXPEDITE DOLLARS]
        , 0 AS [EXPEDITE HOURS]
        , S.PACKLIST_ID + ',' + STUFF(REPLACE('/' + CONVERT(NVARCHAR(15), S.INVOICED_DATE, 101), '/0', '/'), 1, 1, '') 
            + ',FIN,WRAP' AS [PACK SLIPS / DATE]  

        , CASE
            WHEN CB.[PART NUMBER] IS NULL THEN ''
            WHEN CB.[Description] LIKE 'Stanchion%' THEN 'FIXED HOURS'
            WHEN CB.[Description] LIKE 'Cargo Panel%' THEN 'FIXED HOURS'
            ELSE '' 
        END AS [COMMENTS]

        , 0 AS [SPECIAL FEES]

        , CASE
            WHEN CB.[Description] IS NOT NULL THEN 0.
            WHEN LT.[TYPE] = 'S' THEN CONVERT(DECIMAL(14, 4), LT.HOURS_WORKED)
            ELSE 0.
        END AS SORT_SETUP

        , CASE
            WHEN CB.[Description] LIKE 'Stanchion%' THEN [Finish_Unit Run Hours] * COL.TOTAL_SHIPPED_QTY
            WHEN CB.[Description] LIKE 'Cargo Panel%' THEN [Finish_Unit Run Hours] * COL.TOTAL_SHIPPED_QTY
--            WHEN CB.[Description] IS NOT NULL THEN 0.
            WHEN COL.PART_ID LIKE '437W8500%' AND CONVERT(DECIMAL(14, 4), SRI.USER_10) IS NOT NULL
                THEN CONVERT(DECIMAL(14, 4), SRI.USER_10)
            WHEN LT.[TYPE] = 'R' THEN CONVERT(DECIMAL(14, 4), LT.HOURS_WORKED)
            ELSE 0.
        END AS SORT_RUN

--> VERIFICATION FIELDS
        , LT.WORKORDER_BASE_ID
        , LT.HOURS_WORKED
        , COL.PART_ID
        , COL.TOTAL_SHIPPED_QTY
        , LT.[TYPE]
        , LT.TRANSACTION_ID
        , CO.CUSTOMER_ID
        , SRE.RESOURCE_ID AS EXC_RESOURCE_ID
        , SRE.USER_1 AS EXC_SKILLSTYPE
        , SRI.RESOURCE_ID AS INC_RESOURCE_ID
        , SRI.USER_1 AS INC_SKILLSTYPE
        , CONVERT(DECIMAL(14, 4), SRI.USER_10) AS INC_CHARGE
        , SL.SHIPPED_QTY
        , S.INVOICE_ID
        , S.INVOICED_DATE
        , S.PACKLIST_ID
        , WO.ACT_SERVICE_COST
        , CB.[Description]
        , CB.[Batch Qty]
        , CONVERT(DECIMAL(14, 4), [Finish_Unit Run Hours]) AS [Finish_Unit Run Hours]
    FROM dbo.SHIPPER S
    INNER JOIN dbo.SHIPPER_LINE SL
        ON S.PACKLIST_ID = SL.PACKLIST_ID
    INNER JOIN dbo.CUST_ORDER_LINE COL
        ON SL.CUST_ORDER_ID = COL.CUST_ORDER_ID
        AND SL.CUST_ORDER_LINE_NO = COL.LINE_NO
    INNER JOIN dbo.CUSTOMER_ORDER CO
        ON COL.CUST_ORDER_ID = CO.ID
    INNER JOIN dbo.DEMAND_SUPPLY_LINK DSL
        ON COL.CUST_ORDER_ID = DSL.DEMAND_BASE_ID
        AND COL.LINE_NO = DSL.DEMAND_SEQ_NO
    INNER JOIN dbo.WORK_ORDER WO
        ON DSL.SUPPLY_BASE_ID = WO.BASE_ID
        AND DSL.SUPPLY_LOT_ID = WO.LOT_ID
        AND DSL.SUPPLY_SPLIT_ID = WO.SPLIT_ID
    INNER JOIN dbo.LABOR_TICKET LT
        ON WO.BASE_ID = LT.WORKORDER_BASE_ID
        AND WO.LOT_ID = LT.WORKORDER_LOT_ID
        AND WO.SPLIT_ID = LT.WORKORDER_SPLIT_ID
        AND WO.SUB_ID = LT.WORKORDER_SUB_ID
        AND WO.[TYPE] = LT.WORKORDER_TYPE
    INNER JOIN dbo.SHOP_RESOURCE_SITE SRE
        ON LT.RESOURCE_ID = SRE.RESOURCE_ID
        --AND ISNULL(SRE.USER_9, '') != 'BOE BILLING EXCLUDE'
    LEFT OUTER JOIN dbo.SHOP_RESOURCE_SITE SRI
        ON LT.RESOURCE_ID = SRI.RESOURCE_ID
        --AND ISNULL(SRI.USER_9, '') = 'BOE BILLING INCLUDE'
    --LEFT OUTER JOIN CTE_INCLUDE CTE
    --    ON CTE.RESOURCE_ID = SR.RESOURCE_ID
    --    AND COL.PART_ID LIKE '437W8500%'
    LEFT OUTER JOIN [SQL-BI-1].LIVESupplemental.dbo.[BOE_Stanchion_Cargo_Bay] CB
        ON CB.[PART NUMBER] = COL.PART_ID
    WHERE SL.SHIPPED_QTY > 0
        AND SRE.USER_1 != N'M'
        AND CO.CUSTOMER_ID = N'BOE609'
        AND CASE
            WHEN COL.PART_ID = '411N2001-1' THEN 'OK'
            WHEN LT.WORKORDER_BASE_ID LIKE '%R%' THEN 'NOT OK'
            WHEN WO.PART_ID LIKE '%REWORK%' THEN 'NOT OK'
            ELSE 'OK'
        END = 'OK'
        AND (S.INVOICED_DATE >= @STARTDATE AND S.INVOICED_DATE <= @ENDDATE)

--        AND CO.ID = '001761716'

    ) A
GROUP BY SPROC 
    , [COST ITEM]
    , [POS]
    , [PART NUMBER]
    , [Description] 
    , [RAMAC]
    , [BILLED QTY]
    , [EXPEDITE HOURS]
    , [EXPEDITE DOLLARS]
    , [SPECIAL FEES]
    , [PACK SLIPS / DATE]  
    , INVOICE_ID
    , COMMENTS
    , [Finish_Unit Run Hours]
    , [Batch Qty] 

--GO



