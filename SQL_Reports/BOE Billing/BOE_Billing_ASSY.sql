--USE [Datamart]
--GO

--/****** Object:  StoredProcedure [dbo].[usp_BOE_BILLING_ASSY]    Script Date: 8/25/2025 3:29:24 PM ******/
--SET ANSI_NULLS ON
--GO

--SET QUOTED_IDENTIFIER ON
--GO

-- C:\Users\williamt\source\Workspaces\Workspace\IT_TeamProject\SSRS_Master-Dev\Visual Enterprise Financial Reports\BOE_Billing_ASSY.rdl

-- =============================================
-- Author:        traci treffert
-- Create date: 9/17/2019
-- Description:    Boeing Billing for BOE609 ASSY
--        converted from original code in Crystal Reports to be used as SSRS report
--        take the logic/process out of hardcoded CR report and put in SQL for ease of maintenance
--        logic to also be included in Gross Margin processing
-- =============================================

--DECLARE @STARTDATE DATE = '2025-08-18';
--DECLARE @ENDDATE DATE = '2025-08-24';

--CREATE PROCEDURE [dbo].[usp_BOE_BILLING_ASSY]  (@STARTDATE DATE, @ENDDATE DATE)
--AS

SET NOCOUNT ON;
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
SET DEADLOCK_PRIORITY LOW;

DECLARE @CHARGE FLOAT = (SELECT CHARGE FROM [SQL-BI-1].LIVESupplemental.dbo.[BOE_Billing_Charges] WHERE [REPORT] = 'usp_BOE_BILLING_ASSY')
, @SPECIAL FLOAT = (SELECT SPECIAL FROM [SQL-BI-1].LIVESupplemental.dbo.[BOE_Billing_Charges] WHERE [REPORT] = 'usp_BOE_BILLING_ASSY')
, @SERVICE FLOAT = (SELECT [SERVICE] FROM [SQL-BI-1].LIVESupplemental.dbo.[BOE_Billing_Charges] WHERE [REPORT] = 'usp_BOE_BILLING_ASSY')
--, @STARTDATE DATE = '5/1/2020' 
--, @ENDDATE DATE = '8/31/2020'
;

SELECT 'ASSY' AS SPROC 
    , 0 AS [COST ITEM]
    , '1' AS [POS]
    , PART_ID AS [PART NUMBER]
    , ID AS [RAMAC]
    , TOTAL_SHIPPED_QTY AS [BILLED QTY]
    , (((SUM(SORT_SETUP) + SUM(SORT_RUN)) * @CHARGE) / TOTAL_SHIPPED_QTY) AS [UNIT PRICE]
    , (SUM(SORT_SETUP) + SUM(SORT_RUN)) * @CHARGE AS [EXTENDED DOLLARS]
    , 0 AS [EXPEDITE DOLLARS]
    , CASE
        WHEN ACT_SERVICE_COST > 0 THEN @SPECIAL
        ELSE 0
    END AS [SPECIAL FEES]
    , 0 AS SERVICE_FEES
    , SUM(SORT_SETUP) AS [SETUP HOURS]
    , SUM(SORT_RUN) AS [RUN HOURS]
    , 0 AS [EXPEDITE HOURS]
    , PACKLIST_ID + ',' + STUFF(REPLACE('/' + CONVERT(NVARCHAR(15), INVOICED_DATE, 101), '/0', '/'), 1, 1, '') 
        + ',ASSY,WRAP' AS [PACK SLIPS / DATE]  
    , INVOICE_ID AS [INVOICE ID]
    , '' AS [COMMENTS]
    , '' AS [SORTATION]
FROM 
    (SELECT CO.ID
        , LT.WORKORDER_BASE_ID
        , LT.HOURS_WORKED
        , COL.PART_ID
        , COL.TOTAL_SHIPPED_QTY
        , LT.[TYPE]
        , LT.TRANSACTION_ID
        , CO.CUSTOMER_ID
        , SR.USER_1
        , SL.SHIPPED_QTY
        , S.INVOICE_ID
        , S.INVOICED_DATE
        , S.PACKLIST_ID
        , SR.RESOURCE_ID
        , CASE
            WHEN LT.[TYPE] = 'S' THEN LT.HOURS_WORKED
            ELSE 0
        END AS SORT_SETUP
        , COALESCE(CASE
                WHEN LT.[TYPE] = 'R' THEN LT.HOURS_WORKED
                ELSE 0
            END
            , 0) AS SORT_RUN
        , WO.ACT_SERVICE_COST
--        , ROUND((WO.ACT_SERVICE_COST/@SERVICE), 2) AS SERVICE_FEE
    FROM dbo.SHIPPER S
    INNER JOIN dbo.SHIPPER_LINE SL
        ON S.PACKLIST_ID = SL.PACKLIST_ID
    INNER JOIN dbo.CUST_ORDER_LINE COL
        ON SL.CUST_ORDER_ID = COL.CUST_ORDER_ID
        AND SL.CUST_ORDER_LINE_NO = COL.LINE_NO
    INNER JOIN dbo.CUSTOMER_ORDER CO
        ON COL.CUST_ORDER_ID = CO.ID
    INNER JOIN dbo.DEMAND_SUPPLY_LINK DSL
        ON COL.CUST_ORDER_ID = DSL.DEMAND_BASE_ID
        AND COL.LINE_NO = DSL.DEMAND_SEQ_NO
    INNER JOIN dbo.WORK_ORDER WO
        ON DSL.SUPPLY_BASE_ID = WO.BASE_ID
        AND DSL.SUPPLY_LOT_ID = WO.LOT_ID
        AND DSL.SUPPLY_SPLIT_ID = WO.SPLIT_ID
    INNER JOIN dbo.LABOR_TICKET LT
        ON WO.BASE_ID = LT.WORKORDER_BASE_ID
        AND WO.LOT_ID = LT.WORKORDER_LOT_ID
        AND WO.SPLIT_ID = LT.WORKORDER_SPLIT_ID
        AND WO.SUB_ID = LT.WORKORDER_SUB_ID
        AND WO.[TYPE] = LT.WORKORDER_TYPE
    INNER JOIN dbo.SHOP_RESOURCE_SITE SR
        ON LT.RESOURCE_ID = SR.RESOURCE_ID
        --AND ISNULL(SR.USER_9, '') != 'BOE BILLING EXCLUDE'
    WHERE SL.SHIPPED_QTY > 0
        AND SR.USER_1 IN (N'D', N'E', N'M')
        AND CO.CUSTOMER_ID = N'BOE609'
        AND EXISTS (SELECT 1 FROM [sql-bi-1].LIVESupplemental.dbo.ReportSettings_PartID EX
            WHERE COL.PART_ID = EX.PART_ID
                AND REPORT_NAME = 'usp_BOE_BILLING_ASSY'
                AND REPORT_SECTION = 'MAIN INCLUSION'
        )
        AND CASE
            WHEN COL.PART_ID IN ('411N2001-1', '411A2001-1B') THEN 'OK'
            WHEN LT.WORKORDER_BASE_ID LIKE '%R%' THEN 'NOT OK'
            WHEN WO.PART_ID LIKE '%REWORK%' THEN 'NOT OK'
            ELSE 'OK'
        END = 'OK'
        AND (S.INVOICED_DATE >= @STARTDATE AND S.INVOICED_DATE <= @ENDDATE)
    ) A
GROUP BY PART_ID
    , ID
    , TOTAL_SHIPPED_QTY
    , PACKLIST_ID
    , INVOICED_DATE
    , INVOICE_ID
    , ACT_SERVICE_COST



--GO


